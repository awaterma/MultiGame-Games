<?xml version="1.0" encoding="utf-8"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
        creationComplete="init()">
    <fx:Metadata>
        [ResourceBundle("Commons")]
    </fx:Metadata>
    
    <fx:Script>
        <![CDATA[
        import mx.collections.ArrayCollection;
        import mx.ecosur.multigame.entity.Game;
        import mx.messaging.ChannelSet;
        import mx.messaging.Producer;
        import mx.utils.StringUtil;

        import mx.controls.Alert;

        import mx.messaging.messages.AsyncMessage;
        import mx.messaging.messages.IMessage;
        import mx.messaging.events.MessageFaultEvent;

        import mx.ecosur.multigame.entity.GamePlayer;
        import mx.ecosur.multigame.entity.ChatMessage;
        import mx.ecosur.multigame.enum.GameEvent;

        [Bindable]
        public var currentPlayer:GamePlayer;

        [Bindable]
        public var currentGame:Game;

        private var _messages:ArrayCollection;

        private var producer:Producer;

        public function init(channelSet:ChannelSet = null):void {
            producer = new Producer();            
            producer.destination = "multigame-destination";
            if (channelSet) {
                producer.channelSet = channelSet;
            }
        }

         /* Functions */
        private function send():void {
            if (StringUtil.trim(msg.text).length == 0) {
                return;
            }
            var message:IMessage = new AsyncMessage();
            var chatMessage:ChatMessage = new ChatMessage();
            chatMessage.sender = currentPlayer;
            chatMessage.body = msg.text;
            chatMessage.dateSent = new Date();
            message.body = chatMessage;
            message.headers.GAME_ID = currentGame.id;
            message.headers.GAME_EVENT = GameEvent.CHAT;
            producer.send(message);
            msg.text = "";
        }

        public function addMessage(chatMessage:ChatMessage):void {
            if (_messages == null)
                _messages = new ArrayCollection();
            if (_messages.getItemIndex(chatMessage) == -1) {
                log.text += "(" + chatMessage.dateSent.toLocaleTimeString() + ") " + chatMessage.sender.name + ": " + chatMessage.body + "\n";
                callLater(updateScroll);
            }
            _messages.addItem(chatMessage);
        }

        private function faultHandler(event:MessageFaultEvent):void {
            Alert.show("fault in message " + event.faultString);
        }

        private function updateScroll():void {
            if (log.scroller != null && log.scroller.verticalScrollBar != null) {
                log.scroller.verticalScrollBar.value = log.scroller.verticalScrollBar.maximum;
            }
        }
        ]]>
    </fx:Script>
    <s:TextArea id="log" width="100%" heightInLines="10" paddingLeft="0" editable="false"/>
    <s:controlBarContent>
        <s:TextArea id="msg" width="100%" heightInLines="3"/>
        <s:Button id="sendBtn" label="{resourceManager.getString('Commons','chat.panel.button')}" click="send()" color="#ffffff"/>
    </s:controlBarContent>
</s:Panel>
